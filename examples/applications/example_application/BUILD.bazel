load("@rules_apple//apple:apple.bzl", "local_provisioning_profile")
load("@rules_apple//apple:ios.bzl", "ios_application", "ios_unit_test")
load("@rules_apple//apple:versioning.bzl", "apple_bundle_version")
load("@rules_xcodeproj//xcodeproj:defs.bzl", "top_level_target", "xcode_provisioning_profile", "xcodeproj")

# TODO: Update these to your bundle ID and team ID.
TEAM_ID = "9CLW2BBDAG"

BUNDLE_ID = "com.LoganShire.ExampleApplication"

apple_bundle_version(
    name = "_version",
    build_version = "1",
    short_version_string = "1.0",
    tags = ["manual"],
)

local_provisioning_profile(
    name = "_xcode_managed_profile",
    profile_name = "iOS Team Provisioning Profile: " + BUNDLE_ID,
    tags = ["manual"],
    team_id = TEAM_ID,
)

xcode_provisioning_profile(
    name = "_development_profile",
    managed_by_xcode = True,
    provisioning_profile = ":_xcode_managed_profile",
    tags = ["manual"],
)

ios_application(
    name = "example_application",
    bundle_id = BUNDLE_ID,
    bundle_name = "ExampleApplication",
    families = [
        "iphone",
        "ipad",
    ],
    infoplists = [":Info.plist"],
    launch_storyboard = ":LaunchScreen.storyboard",
    minimum_os_version = "17.0",
    provisioning_profile = ":_development_profile",
    version = ":_version",
    visibility = ["//visibility:public"],
    deps = ["//examples/applications/example_application/sources"],
)

ios_unit_test(
    name = "example_application_tests",
    bundle_id = BUNDLE_ID + ".Tests",
    minimum_os_version = "17.0",
    test_host = ":example_application",
    visibility = ["//visibility:public"],
    deps = ["//saber/saber_plugin_tests"],
)

xcodeproj(
    name = "example_application_project",
    adjust_schemes_for_swiftui_previews = True,
    build_mode = "bazel",
    project_name = "ExampleApplication",
    tags = ["manual"],
    top_level_targets = [
        top_level_target(
            ":example_application",
            target_environments = [
                "device",
                "simulator",
            ],
        ),
        top_level_target(
            "//examples/applications/example_application/previews:example_application_previews",
            target_environments = [
                "simulator",
            ],
        ),
        ":example_application_tests",
    ],
)
